;===========================================================;
;                       vga interface			    		;
;===========================================================;

;VESA 640 x 480 @ 75 Hz
;sync polarity -/-

;4 clocks per pixel -> 126 MHz
;irq0 vertical clock
;irq1 blank_space
;irq7 starting impulse

;-----------------------------------------------------------
;vga_1: output pixels + horizontal timing	13 instructions

.program vga_1

.sideset 1
irq wait 7										//wait for start signal

.wrap_target
h_sync_pulse:
	out x, 7					side 0	[3]		//[4] get sync_pulse_counter -2
h_sync_pulse_loop:
	jmp x-- h_sync_pulse_loop	side 0	[3]		//[4][N+1] sync_pulse_loop


back_porch:
	out x, 7					side 1 	[1]		//[2] get back_porch_counter -4
back_porch_loop:
	jmp x-- back_porch_loop		side 1	[3]		//[4][N+1] back_porch_loop
	irq 0						side 1	[3]		//[1] vertical clock signal
	wait 1 irq 1				side 1			//[1] check whether bottom of screen is reached
	

pixels:
	out x, 10					side 1	[3]		//[4] get pixel_counter -1 
pixels_loop:
	out pins, 8					side 1	[2]		//[3] output pixel data
	jmp x-- pixels_loop			side 1			//[1][N+1] loop for one line of pixels


front_porch:
	out x, 5					side 1	[3]		//[4] get front_porch_counter -2
front_porch_loop:
	jmp x-- front_porch_loop	side 1	[3]		//[4][N+1]
.wrap


% c-sdk {

%}

;-----------------------------------------------------------
;vga_2: 
.program vga_2

.sideset 1
irq clear 7                            			//set starting impulse

.wrap_target
lines:
	out x, 10					side 1			//get line_counter -1
lines_loop:
	wait 1 irq 0				side 1			//wait for vertical clock signal
	jmp x-- lines_loop			side 1			//lines_loop


front_porch:
	irq 1						side 1			//set blank space 
	wait 1 irq 0				side 1			//wait for vertical clock


v_sync_pulse:
	out x, 2					side 0			//get sync_pulse_counter -1
v_sync_pulse_loop:
	wait 1 irq 0				side 0			//wait for vertical clock
	jmp x-- v_sync_pulse_loop	side 0			//v_sync_pulse_loop


back_porch:
	out x, 5					side 1			//get back_porch_counter -1
back_porch_loop:
	wait 1 irq 0				side 1			//wait for vertical_clock
	jmp x-- back_porch_loop		side 1			//loop
	
irq clear 1
.wrap


% c-sdk {

%}
